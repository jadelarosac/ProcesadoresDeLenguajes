\section{Descripci√≥n formal de la sintaxis del lenguaje}

\begin{verbatim}
<Programa>                    ::= <Declar_de_subprogs>
                                  <Cabecera_programa>\n<bloque>
<bloque>                      ::= <Inicio_de_bloque>
                                  <Sentencias>
                                  <Fin_de_bloque>\n
<Declar_subprogs>             ::= <Declar_subprog>
                                  | <Declar_subprog><Declar_subprogs>
                                  | \n<Declar_subprogs>
                                  | <Declar_subprogs>\n
<Declar_subprog>              ::= <Cabecera_subprograma>\n<bloque>
<Declar_de_variables_locales> ::= <tipo> <Variables_locales>;
<Cabecera_programa>           ::= programa_principal()
<Inicio_de_bloque>            ::= {\n
<Fin_de_bloque>               ::= \n}
<elemento_de_array>           ::= <identificador>[<expresion>]
                                  | <identificador>[<expresion>, <expresion>]
<variable>                    ::= <identificador>
                                  | <elemento_de_array>
<Variables_locales>           ::= <lista_variables>
                                  | <lista_variables>, <sentencia_asignacion>
                                  | <sentencia_asignacion>
<Cabecera_subprog>            ::= <identificador>(<argumentos>)
                                  | <identificador>()
<argumentos>                  ::= <tipo> <identificador>
                                  | <tipo> <identificador>, <argumentos>
<Sentencias>                  ::= \t<Sentencias>
                                  | <Sentencia>\n<Sentencias>
                                  | <Sentencia>
<Sentencia>                   ::= <Declar_de_variables_locales>
                                  | <sentencia_asignacion>
                                  | <sentencia_si>
                                  | <sentencia_mientras>
                                  | <sentencia_hacer_hasta>
                                  | <sentencia_entrada>
                                  | <sentencia_salida>
                                  | <sentencia_retornar>
                                  | \n
<sentencia_asignacion>        ::= <variable> = <expresion>;
<sentencia_si>                ::= si (<expresion>) <bloque>
<sentencia_mientras>          ::= mientras (<expresion>) <bloque>
<sentencia_hacer_hasta>       ::= hacer <bloque> hasta que (<expresion>);
<sentencia_entrada>           ::= leer <lista_variables>;
<sentencia_salida>            ::= escribir <lista_expresiones>;
<sentencia_retornar>          ::= retornar <expresion>;
<lista_variables>             ::= <variable>
                                  | <variable>, <lista_variables>
<lista_expresiones>           ::= <expresion>
                                  | <expresion>, <lista_expresiones>
<expresion>                   ::= ( <expresion> )
                                  | <op_unario> <expresion>
                                  | <expresion> <op_binario> <expresion>
                                  | <variable>
                                  | <constante>
                                  | <funcion>
<funcion>                     ::= <identificador>()
                                  | <identificador>(<lista_variables>)
<tipo>                        ::= <tipo_elemental>
                                  | <tipo_elemental> vector[<expresion>]
                                  | <tipo_elemental>
                                  matriz[<expresion>, <expresion>]
<tipo_elemental>              ::= entero | real | caracter | booleano
<op_binario>                  ::= + | - | * | / | ** | && | || | == | !=
                                  | <= | >= | < | >
<op_unario>                   ::= - | !
<caracter>                    ::= [a-z] | [A-Z]
<digito>                      ::= [0-9]
<numero>                      ::= <digito> | <digito><numero>
<numero_en_coma_flotante>     ::= <numero>.<numero>
<cadena>                      ::= <caracter> | <caracter><cadena>
<identificador>               ::= <identificador><numero>
                                  | <identificador><cadena>
                                  | <cadena>
<constante>                   ::= <numero>
                                  | <numero_en_coma_flotante>
                                  | "<cadena>"
                                  | true
                                  | false
                                  | <elemento_de_array>
                                  | [lista_expresiones]
                                  | [lista_expresiones; lista_expresiones]
\end{verbatim}


